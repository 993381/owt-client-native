import("//webrtc/build/webrtc.gni")

if (is_ios) {
  import("//build/config/ios/rules.gni")
}

declare_args() {
  include_internal_audio_device = true
}

static_library("woogeen"){
  deps = [
    ":woogeen_sdk_base",
    ":woogeen_sdk_p2p",
    ":woogeen_sdk_conf",
    "//third_party/protobuf:protobuf_lite",
    "//webrtc",
    "//webrtc/system_wrappers:system_wrappers_default",
  ]
  complete_static_lib = true
}

source_set("woogeen_sdk_base") {
  sources = [
    "sdk/base/peerconnectiondependencyfactory.h",
    "sdk/base/peerconnectiondependencyfactory.cc",
    "sdk/base/eventtrigger.h",
    "sdk/base/functionalobserver.h",
    "sdk/base/functionalobserver.cc",
    "sdk/base/mediaconstraintsimpl.h",
    "sdk/base/stream.cc",
    "sdk/base/localcamerastreamparameters.cc",
    "sdk/base/exception.cc",
    "sdk/base/peerconnectionchannel.h",
    "sdk/base/peerconnectionchannel.cc",
    "sdk/base/encodedvideoencoder.h",
    "sdk/base/encodedvideoencoder.cc",
    "sdk/base/encodedvideoencoderfactory.h",
    "sdk/base/encodedvideoencoderfactory.cc",
    "sdk/base/globalconfiguration.cc",
    "sdk/base/webrtcvideorendererimpl.h",
    "sdk/base/webrtcvideorendererimpl.cc",
    "sdk/base/customizedframescapturer.cc",
    "sdk/base/customizedframescapturer.h",
    "sdk/base/sdputils.h",
    "sdk/base/sdputils.cc",
    "sdk/base/mediautils.h",
    "sdk/base/mediautils.cc",
    "sdk/base/deviceutils.cc",
    "sdk/base/logging.cc",
    "sdk/base/sysinfo.h",
    "sdk/base/sysinfo.cc",
    "sdk/include/cpp/woogeen/base/stream.h",
    "sdk/include/cpp/woogeen/base/clientconfiguration.h",
    "sdk/include/cpp/woogeen/base/localcamerastreamparameters.h",
    "sdk/include/cpp/woogeen/base/exception.h",
    "sdk/include/cpp/woogeen/base/videorendererinterface.h",
    "sdk/include/cpp/woogeen/base/framegeneratorinterface.h",
    "sdk/include/cpp/woogeen/base/connectionstats.h",
    "sdk/include/cpp/woogeen/base/deviceutils.h",
    "sdk/include/cpp/woogeen/base/logging.h",
  ]

  public_deps = [
    "//webrtc/api:libjingle_peerconnection",
    "//third_party/libyuv:libyuv",
  ]

  include_dirs = [
    "sdk/include/cpp",
    "include/msdk",
  ]

  defines = [
    "WEBRTC_INCLUDE_INTERNAL_AUDIO_DEVICE",
  ]

  if (is_win) {
    sources += [
      "sdk/base/win/mftmediadecoder.h",
      "sdk/base/win/mftmediadecoder.cpp",
      "sdk/base/win/mftvideodecoderfactory.cpp",
      "sdk/base/win/mftvideodecoderfactory.h",
      "sdk/base/win/d3dvideorenderer.h",
      "sdk/base/win/d3dvideorenderer.cpp",
      "sdk/base/win/d3dnativeframe.h",
      "sdk/base/win/d3dnativeframe.cpp",
      "sdk/base/win/mftvideoencoderfactory.cpp",
      "sdk/base/win/mftvideoencoderfactory.h",
      "sdk/base/win/h264_video_mft_encoder.cpp",
      "sdk/base/win/h264_video_mft_encoder.h",
      "sdk/base/win/h264_msdk_decoder.cpp",
      "sdk/base/win/h264_msdk_decoder.h",
    ]

    cflags = [
      # TODO: Bug 799, 800
      "/wd4189",  # Unused variables
      "/wd4267",  # Conversion truncation
      "/wd4389",  # Signed/unsigned mismatch
    ]

    if (rtc_use_h265) {
      sources += [
        "sdk/base/win/h265_msdk_decoder.cpp",
        "sdk/base/win/h265_msdk_decoder.h",
        "sdk/base/win/h265_msdk_encoder.cpp",
        "sdk/base/win/h265_msdk_encoder.h",
      ]
    }
  }

  if (is_linux) {
    sources += [
      "sdk/base/linux/v4l2videodecoderfactory.h",
      "sdk/base/linux/v4l2videodecoderfactory.cc",
      "sdk/base/linux/v4l2videodecoder.h",
      "sdk/base/linux/v4l2videodecoder.cc",
    ]
  }

  if (include_internal_audio_device) {
    sources += [
      "sdk/base/customizedaudiodevicemodule.h",
      "sdk/base/customizedaudiodevicemodule.cc",
      "sdk/base/customizedaudiocapturer.h",
      "sdk/base/customizedaudiocapturer.cc",
    ]
  }

  if(is_clang){
    # Bug 804. Fix Chromium style checker warnings.
    configs -= [ "//build/config/clang:find_bad_constructs"]
  }
}

source_set("woogeen_sdk_p2p") {
  deps = [
    ":woogeen_sdk_base",
    "//third_party/jsoncpp:jsoncpp",
  ]

  include_dirs = ["sdk/include/cpp"]

  sources = [
    "sdk/p2p/p2ppeerconnectionchannel.h",
    "sdk/p2p/p2ppeerconnectionchannel.cc",
    "sdk/p2p/p2ppeerconnectionchannelobservercppimpl.h",
    "sdk/p2p/p2ppeerconnectionchannelobservercppimpl.cc",
    "sdk/p2p/peerclient.cc",
    "sdk/p2p/p2pexception.cc",
    "sdk/include/cpp/woogeen/p2p/p2psignalingreceiverinterface.h",
    "sdk/include/cpp/woogeen/p2p/p2psignalingsenderinterface.h",
    "sdk/include/cpp/woogeen/p2p/p2pexception.h",
    "sdk/include/cpp/woogeen/p2p/p2psignalingchannelinterface.h",
    "sdk/include/cpp/woogeen/p2p/peerclient.h",
  ]

  if(is_clang){
    # Bug 804. Fix Chromium style checker warnings.
    configs -= [ "//build/config/clang:find_bad_constructs" ]
  }
}

source_set("woogeen_sdk_conf") {
  deps = [
    ":woogeen_sdk_base",
    "//third_party/jsoncpp:jsoncpp",
  ]

  include_dirs = ["sdk/include/cpp"]

  sources = [
    "sdk/conference/conferenceexception.cc",
    "sdk/conference/conferenceclient.cc",
    "sdk/conference/conferencepeerconnectionchannel.h",
    "sdk/conference/conferencepeerconnectionchannel.cc",
    "sdk/conference/conferencesocketsignalingchannel.h",
    "sdk/conference/conferencesocketsignalingchannel.cc",
    "sdk/conference/remotemixedstream.cc",
    "sdk/include/cpp/woogeen/conference/conferenceexception.h",
    "sdk/include/cpp/woogeen/conference/conferenceclient.h",
    "sdk/include/cpp/woogeen/conference/user.h",
    "sdk/include/cpp/woogeen/conference/remotemixedstream.h",
  ]

  if(is_clang){
    # Bug 804. Fix Chromium style checker warnings.
    configs -= [ "//build/config/clang:find_bad_constructs"]
  }
}

if (is_ios) {
  static_library("woogeen_sdk_objc") {
    deps = [
      ":woogeen_sdk_base",
      ":woogeen_sdk_p2p",
      ":woogeen_sdk_conf",
      "//third_party/jsoncpp",
      "//webrtc/sdk:rtc_sdk_peerconnection_objc",
      "//webrtc/system_wrappers:system_wrappers_default",
    ]

    include_dirs = [
      "//webrtc/sdk/objc/Framework/Headers/WebRTC",
      "sdk/include/cpp",
      "sdk/include/objc",
    ]

    configs += ["//build/config/compiler:enable_arc"]

    sources = [
      "sdk/base/objc/public/RTCConnectionStats.h",
      "sdk/base/objc/public/RTCLocalStream.h",
      "sdk/base/objc/public/RTCErrors.h",
      "sdk/base/objc/public/RTCStream.h",
      "sdk/base/objc/public/RTCRemoteStream.h",
      "sdk/base/objc/public/RTCLocalCameraStream.h",
      "sdk/base/objc/public/RTCLocalCameraStreamParameters.h",
      "sdk/base/objc/public/RTCLocalCustomizedStream.h",
      "sdk/base/objc/public/RTCLocalCustomizedStreamParameters.h",
      "sdk/base/objc/public/RTCMediaCodec.h",
      "sdk/base/objc/public/RTCMediaFormat.h",
      "sdk/base/objc/public/RTCRemoteCameraStream.h",
      "sdk/base/objc/public/RTCRemoteScreenStream.h",
      "sdk/base/objc/public/RTCGlobalConfiguration.h",
      "sdk/base/objc/public/RTCFrameGeneratorProtocol.h",
      "sdk/base/objc/AVFoundationVideoCapturerFactory.h",
      "sdk/base/objc/AVFoundationVideoCapturerFactory.mm",
      "sdk/base/objc/RTCConnectionStats+Internal.h",
      "sdk/base/objc/RTCConnectionStats.mm",
      "sdk/base/objc/RTCPeerConnectionDependencyFactory.h",
      "sdk/base/objc/RTCPeerConnectionDependencyFactory.mm",
      "sdk/base/objc/RTCErrors.m",
      "sdk/base/objc/RTCStream+Internal.h",
      "sdk/base/objc/RTCStream.mm",
      "sdk/base/objc/RTCLocalCameraStream.mm",
      "sdk/base/objc/RTCLocalCameraStreamParameters.mm",
      "sdk/base/objc/RTCLocalCameraStreamParameters+Internal.h",
      "sdk/base/objc/RTCLocalCustomizedStream.mm",
      "sdk/base/objc/RTCLocalCustomizedStreamParameters.mm",
      "sdk/base/objc/RTCLocalCustomizedStreamParameters+Internal.h",
      "sdk/base/objc/RTCLocalStream.mm",
      "sdk/base/objc/RTCLocalStream+Internal.h",
      "sdk/base/objc/RTCMediaCodec+Internal.h",
      "sdk/base/objc/RTCMediaCodec.mm",
      "sdk/base/objc/RTCMediaFormat+Internal.h",
      "sdk/base/objc/RTCMediaFormat.mm",
      "sdk/base/objc/RTCRemoteStream.mm",
      "sdk/base/objc/RTCRemoteCameraStream.mm",
      "sdk/base/objc/RTCRemoteScreenStream.mm",
      "sdk/base/objc/RTCGlobalConfiguration.mm",
      "sdk/base/objc/FrameGeneratorObjcImpl.h",
      "sdk/base/objc/FrameGeneratorObjcImpl.mm",
      "sdk/p2p/objc/public/RTCP2PErrors.h",
      "sdk/p2p/objc/public/RTCP2PPeerConnectionChannelObserver.h",
      "sdk/p2p/objc/public/RTCP2PSignalingSenderProtocol.h",
      "sdk/p2p/objc/public/RTCP2PSignalingReceiverProtocol.h",
      "sdk/p2p/objc/public/RTCPeerClient.h",
      "sdk/p2p/objc/public/RTCPeerClientConfiguration.h",
      "sdk/p2p/objc/public/RTCPeerClientObserver.h",
      "sdk/p2p/objc/public/RTCP2PSignalingChannelProtocol.h",
      "sdk/p2p/objc/P2PPeerConnectionChannelObserverObjcImpl.h",
      "sdk/p2p/objc/P2PPeerConnectionChannelObserverObjcImpl.mm",
      "sdk/p2p/objc/RTCP2PPeerConnectionChannel.h",
      "sdk/p2p/objc/RTCP2PPeerConnectionChannel.mm",
      "sdk/p2p/objc/RTCP2PSignalingSenderObjcImpl.h",
      "sdk/p2p/objc/RTCP2PSignalingSenderObjcImpl.mm",
      "sdk/p2p/objc/RTCPeerClient.m",
      "sdk/p2p/objc/RTCPeerClientConfiguration.m",
      "sdk/conference/objc/public/RTCConferenceClient.h",
      "sdk/conference/objc/public/RTCConferenceClientConfiguration.h",
      "sdk/conference/objc/public/RTCConferenceClientObserver.h",
      "sdk/conference/objc/public/RTCRemoteMixedStream.h",
      "sdk/conference/objc/public/RTCConferenceErrors.h",
      "sdk/conference/objc/public/RTCConferenceUser.h",
      "sdk/conference/objc/public/RTCConferenceSubscribeOptions.h",
      "sdk/conference/objc/public/RTCRemoteMixedStreamObserver.h",
      "sdk/conference/objc/RTCConferenceSubscribeOptions+Internal.h",
      "sdk/conference/objc/RTCConferenceSubscribeOptions.mm",
      "sdk/conference/objc/RTCRemoteMixedStream.mm",
      "sdk/conference/objc/RTCConferenceClient.mm",
      "sdk/conference/objc/RTCConferenceClientConfiguration.mm",
      "sdk/conference/objc/RTCConferenceUser+Internal.h",
      "sdk/conference/objc/RTCConferenceUser.mm",
      "sdk/conference/objc/ConferenceClientObserverObjcImpl.h",
      "sdk/conference/objc/ConferenceClientObserverObjcImpl.mm",
      "sdk/conference/objc/RemoteMixedStreamObserverObjcImpl.h",
      "sdk/conference/objc/RemoteMixedStreamObserverObjcImpl.mm",
    ]

    # Bug 805.
    cflags = ["-Wno-nullability-completeness"]

    if(is_clang){
      # Bug 804. Fix Chromium style checker warnings.
      configs -= [ "//build/config/clang:find_bad_constructs"]
    }

    complete_static_lib = true
  }
}
