#
# Intel License
#

import("//webrtc/webrtc.gni")

if (is_ios) {
  import("//build/config/ios/rules.gni")
}

declare_args() {
  include_internal_audio_device = true
}

if (is_ios || is_mac) {
  config("woogeen_sdk_objc_warnings_config") {
    # Bug 1375. Fix -Wobjc-missing-property-synthesis warnings.
    cflags_objc = [ "-Wno-objc-missing-property-synthesis" ]
    cflags_objcc = [ "-Wno-objc-missing-property-synthesis" ]
  }
}

static_library("woogeen") {
  deps = [
    ":woogeen_sdk_base",
    ":woogeen_sdk_conf",
    ":woogeen_sdk_p2p",
    "//third_party/protobuf:protobuf_lite",
    "//webrtc",
    "//webrtc/system_wrappers:system_wrappers_default",
  ]

  if (is_ios) {
    deps += [ ":woogeen_sdk_objc" ]
  }
  complete_static_lib = true
}

source_set("woogeen_sdk_base") {
  sources = [
    "sdk/base/customizedframescapturer.cc",
    "sdk/base/customizedframescapturer.h",
    "sdk/base/customizedvideoencoderproxy.cc",
    "sdk/base/customizedvideoencoderproxy.h",
    "sdk/base/desktopcapturer.cc",
    "sdk/base/desktopcapturer.h",
    "sdk/base/deviceutils.cc",
    "sdk/base/encodedvideoencoderfactory.cc",
    "sdk/base/encodedvideoencoderfactory.h",
    "sdk/base/eventtrigger.h",
    "sdk/base/exception.cc",
    "sdk/base/functionalobserver.cc",
    "sdk/base/functionalobserver.h",
    "sdk/base/globalconfiguration.cc",
    "sdk/base/localcamerastreamparameters.cc",
    "sdk/base/logging.cc",
    "sdk/base/mediaconstraintsimpl.h",
    "sdk/base/mediautils.cc",
    "sdk/base/mediautils.h",
    "sdk/base/peerconnectionchannel.cc",
    "sdk/base/peerconnectionchannel.h",
    "sdk/base/peerconnectiondependencyfactory.cc",
    "sdk/base/peerconnectiondependencyfactory.h",
    "sdk/base/sdputils.cc",
    "sdk/base/sdputils.h",
    "sdk/base/stream.cc",
    "sdk/base/sysinfo.cc",
    "sdk/base/sysinfo.h",
    "sdk/base/webrtcvideorendererimpl.cc",
    "sdk/base/webrtcvideorendererimpl.h",
    "sdk/base/windowcapturer.cc",
    "sdk/base/windowcapturer.h",
    "sdk/include/cpp/woogeen/base/clientconfiguration.h",
    "sdk/include/cpp/woogeen/base/connectionstats.h",
    "sdk/include/cpp/woogeen/base/deviceutils.h",
    "sdk/include/cpp/woogeen/base/exception.h",
    "sdk/include/cpp/woogeen/base/framegeneratorinterface.h",
    "sdk/include/cpp/woogeen/base/localcamerastreamparameters.h",
    "sdk/include/cpp/woogeen/base/logging.h",
    "sdk/include/cpp/woogeen/base/stream.h",
    "sdk/include/cpp/woogeen/base/videodecoderinterface.h",
    "sdk/include/cpp/woogeen/base/videorendererinterface.h",
  ]

  public_deps = [
    "//third_party/libyuv:libyuv",
    "//webrtc/api:libjingle_peerconnection",
    "//webrtc/base:rtc_json",
    "//webrtc/modules/desktop_capture:desktop_capture",
  ]

  include_dirs = [
    "sdk/include/cpp",
    "include/msdk",
  ]

  defines = [ "WEBRTC_INCLUDE_INTERNAL_AUDIO_DEVICE" ]

  if (is_ios) {
    sources += [
      "sdk/base/ios/networkmonitorios.cc",
      "sdk/base/ios/networkmonitorios.h",
    ]
  }

  if (is_win) {
    sources += [
      "sdk/base/win/d3dnativeframe.h",
      "sdk/base/win/external_msdk_decoder.cpp",
      "sdk/base/win/external_msdk_decoder.h",
      "sdk/base/win/h264_video_mft_encoder.cpp",
      "sdk/base/win/h264_video_mft_encoder.h",
      "sdk/base/win/mftmediadecoder.cpp",
      "sdk/base/win/mftmediadecoder.h",
      "sdk/base/win/mftvideodecoderfactory.cpp",
      "sdk/base/win/mftvideodecoderfactory.h",
      "sdk/base/win/mftvideoencoderfactory.cpp",
      "sdk/base/win/mftvideoencoderfactory.h",
      "sdk/base/win/videorendererwin.cpp",
      "sdk/base/win/videorendererwin.h",
    ]

    if (rtc_use_h265) {
      sources += [
        "sdk/base/win/h265_msdk_decoder.cpp",
        "sdk/base/win/h265_msdk_decoder.h",
        "sdk/base/win/h265_msdk_encoder.cpp",
        "sdk/base/win/h265_msdk_encoder.h",
      ]
    }
  }

  if (!is_ios) {
    sources += [
      "sdk/base/customizedvideodecoderfactory.cc",
      "sdk/base/customizedvideodecoderfactory.h",
      "sdk/base/customizedvideodecoderproxy.cc",
      "sdk/base/customizedvideodecoderproxy.h",
    ]
  }

  if (include_internal_audio_device) {
    sources += [
      "sdk/base/customizedaudiocapturer.cc",
      "sdk/base/customizedaudiocapturer.h",
      "sdk/base/customizedaudiodevicemodule.cc",
      "sdk/base/customizedaudiodevicemodule.h",
    ]
  }


  if (is_linux && is_clang) {
    configs -= [ "//build/config/clang:find_bad_constructs" ]
  }
}

source_set("woogeen_sdk_p2p") {
  deps = [
    ":woogeen_sdk_base",
    "//third_party/jsoncpp:jsoncpp",
  ]

  include_dirs = [ "sdk/include/cpp" ]

  sources = [
    "sdk/include/cpp/woogeen/p2p/p2pexception.h",
    "sdk/include/cpp/woogeen/p2p/p2psignalingchannelinterface.h",
    "sdk/include/cpp/woogeen/p2p/p2psignalingreceiverinterface.h",
    "sdk/include/cpp/woogeen/p2p/p2psignalingsenderinterface.h",
    "sdk/include/cpp/woogeen/p2p/peerclient.h",
    "sdk/p2p/p2pexception.cc",
    "sdk/p2p/p2ppeerconnectionchannel.cc",
    "sdk/p2p/p2ppeerconnectionchannel.h",
    "sdk/p2p/p2ppeerconnectionchannelobservercppimpl.cc",
    "sdk/p2p/p2ppeerconnectionchannelobservercppimpl.h",
    "sdk/p2p/peerclient.cc",
  ]


  if (is_linux && is_clang) {
    configs -= [ "//build/config/clang:find_bad_constructs" ]
  }
}

source_set("woogeen_sdk_conf") {
  deps = [
    ":woogeen_sdk_base",
    "//third_party/jsoncpp:jsoncpp",
  ]

  include_dirs = [ "sdk/include/cpp" ]

  sources = [
    "sdk/conference/conferenceclient.cc",
    "sdk/conference/conferenceexception.cc",
    "sdk/conference/conferencepeerconnectionchannel.cc",
    "sdk/conference/conferencepeerconnectionchannel.h",
    "sdk/conference/conferencesocketsignalingchannel.cc",
    "sdk/conference/conferencesocketsignalingchannel.h",
    "sdk/conference/remotemixedstream.cc",
    "sdk/include/cpp/woogeen/conference/conferenceclient.h",
    "sdk/include/cpp/woogeen/conference/conferenceexception.h",
    "sdk/include/cpp/woogeen/conference/remotemixedstream.h",
    "sdk/include/cpp/woogeen/conference/user.h",
  ]


  if (is_linux && is_clang) {
    configs -= [ "//build/config/clang:find_bad_constructs" ]
  }
}

if (is_ios) {
  source_set("woogeen_sdk_objc") {
    deps = [
      ":woogeen_sdk_base",
      ":woogeen_sdk_conf",
      ":woogeen_sdk_p2p",
      "//third_party/jsoncpp",
      "//webrtc/sdk:rtc_sdk_peerconnection_objc",
      "//webrtc/system_wrappers:system_wrappers_default",
    ]

    include_dirs = [
      "//webrtc/sdk/objc/Framework/Headers/WebRTC",
      "sdk/include/cpp",
      "sdk/include/objc",
    ]

    configs += [
      "//build/config/compiler:enable_arc",
      ":woogeen_sdk_objc_warnings_config",
    ]

    sources = [
      "sdk/base/objc/AVFoundationVideoCapturerFactory.h",
      "sdk/base/objc/AVFoundationVideoCapturerFactory.mm",
      "sdk/base/objc/FrameGeneratorObjcImpl.h",
      "sdk/base/objc/FrameGeneratorObjcImpl.mm",
      "sdk/base/objc/RTCAVFoundationVideoSource+Woogeen.mm",
      "sdk/base/objc/RTCConnectionStats+Internal.h",
      "sdk/base/objc/RTCConnectionStats.mm",
      "sdk/base/objc/RTCErrors.m",
      "sdk/base/objc/RTCGlobalConfiguration.mm",
      "sdk/base/objc/RTCLocalCameraStream.mm",
      "sdk/base/objc/RTCLocalCameraStreamParameters+Internal.h",
      "sdk/base/objc/RTCLocalCameraStreamParameters.mm",
      "sdk/base/objc/RTCLocalCustomizedStream.mm",
      "sdk/base/objc/RTCLocalCustomizedStreamParameters+Internal.h",
      "sdk/base/objc/RTCLocalCustomizedStreamParameters.mm",
      "sdk/base/objc/RTCLocalStream+Internal.h",
      "sdk/base/objc/RTCLocalStream.mm",
      "sdk/base/objc/RTCMediaCodec+Internal.h",
      "sdk/base/objc/RTCMediaCodec.mm",
      "sdk/base/objc/RTCMediaFormat+Internal.h",
      "sdk/base/objc/RTCMediaFormat.mm",
      "sdk/base/objc/RTCPeerConnectionDependencyFactory.h",
      "sdk/base/objc/RTCPeerConnectionDependencyFactory.mm",
      "sdk/base/objc/RTCPeerConnectionFactory+Woogeen.h",
      "sdk/base/objc/RTCPeerConnectionFactory+Woogeen.mm",
      "sdk/base/objc/RTCRemoteCameraStream.mm",
      "sdk/base/objc/RTCRemoteScreenStream.mm",
      "sdk/base/objc/RTCRemoteStream.mm",
      "sdk/base/objc/RTCStream+Internal.h",
      "sdk/base/objc/RTCStream.mm",
      "sdk/base/objc/public/RTCAVFoundationVideoSource+Woogeen.h",
      "sdk/base/objc/public/RTCConnectionStats.h",
      "sdk/base/objc/public/RTCErrors.h",
      "sdk/base/objc/public/RTCFrameGeneratorProtocol.h",
      "sdk/base/objc/public/RTCGlobalConfiguration.h",
      "sdk/base/objc/public/RTCLocalCameraStream.h",
      "sdk/base/objc/public/RTCLocalCameraStreamParameters.h",
      "sdk/base/objc/public/RTCLocalCustomizedStream.h",
      "sdk/base/objc/public/RTCLocalCustomizedStreamParameters.h",
      "sdk/base/objc/public/RTCLocalStream.h",
      "sdk/base/objc/public/RTCMediaCodec.h",
      "sdk/base/objc/public/RTCMediaFormat.h",
      "sdk/base/objc/public/RTCRemoteCameraStream.h",
      "sdk/base/objc/public/RTCRemoteScreenStream.h",
      "sdk/base/objc/public/RTCRemoteStream.h",
      "sdk/base/objc/public/RTCStream.h",
      "sdk/base/sysinfoios.mm",
      "sdk/conference/objc/ConferenceClientObserverObjcImpl.h",
      "sdk/conference/objc/ConferenceClientObserverObjcImpl.mm",
      "sdk/conference/objc/RTCConferenceClient.mm",
      "sdk/conference/objc/RTCConferenceClientConfiguration.mm",
      "sdk/conference/objc/RTCConferenceSubscribeOptions+Internal.h",
      "sdk/conference/objc/RTCConferenceSubscribeOptions.mm",
      "sdk/conference/objc/RTCConferenceUser+Internal.h",
      "sdk/conference/objc/RTCConferenceUser.mm",
      "sdk/conference/objc/RTCRemoteMixedStream.mm",
      "sdk/conference/objc/RemoteMixedStreamObserverObjcImpl.h",
      "sdk/conference/objc/RemoteMixedStreamObserverObjcImpl.mm",
      "sdk/conference/objc/public/RTCConferenceClient.h",
      "sdk/conference/objc/public/RTCConferenceClientConfiguration.h",
      "sdk/conference/objc/public/RTCConferenceClientObserver.h",
      "sdk/conference/objc/public/RTCConferenceErrors.h",
      "sdk/conference/objc/public/RTCConferenceSubscribeOptions.h",
      "sdk/conference/objc/public/RTCConferenceUser.h",
      "sdk/conference/objc/public/RTCRemoteMixedStream.h",
      "sdk/conference/objc/public/RTCRemoteMixedStreamObserver.h",
      "sdk/p2p/objc/P2PPeerConnectionChannelObserverObjcImpl.h",
      "sdk/p2p/objc/P2PPeerConnectionChannelObserverObjcImpl.mm",
      "sdk/p2p/objc/RTCP2PPeerConnectionChannel.h",
      "sdk/p2p/objc/RTCP2PPeerConnectionChannel.mm",
      "sdk/p2p/objc/RTCP2PSignalingSenderObjcImpl.h",
      "sdk/p2p/objc/RTCP2PSignalingSenderObjcImpl.mm",
      "sdk/p2p/objc/RTCPeerClient.m",
      "sdk/p2p/objc/RTCPeerClientConfiguration.m",
      "sdk/p2p/objc/public/RTCP2PErrors.h",
      "sdk/p2p/objc/public/RTCP2PPeerConnectionChannelObserver.h",
      "sdk/p2p/objc/public/RTCP2PSignalingChannelProtocol.h",
      "sdk/p2p/objc/public/RTCP2PSignalingReceiverProtocol.h",
      "sdk/p2p/objc/public/RTCP2PSignalingSenderProtocol.h",
      "sdk/p2p/objc/public/RTCPeerClient.h",
      "sdk/p2p/objc/public/RTCPeerClientConfiguration.h",
      "sdk/p2p/objc/public/RTCPeerClientObserver.h",
    ]
  }
}
